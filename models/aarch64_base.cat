(* A baseline Arm-A non-mixed-size multi-copy atomic model 
 * using the armv9 sail concurrency interface
 *
 * with surgery on `obs` and `bob` for easy extension by ifetch/vmsa models.
 *)

"Arm-A base"

include "cos.cat"
include "arm-common.cat"

(* might-be speculatively executed *)
let speculative =
    ctrl
  | addr; po

(* observed by *)
let obs = rfe | fr | co

(* dependency-ordered-before *)
let dob =
    addr | data
  | speculative ; [W]
  | speculative ; [ISB]
  | (addr | data); rfi

(* atomic-ordered-before *)
let aob =
    rmw
  | [range(rmw)]; rfi; [A | Q]

(* barrier-ordered-before *)
let bob =
    [R] ; po ; [dmbld]
  | [W] ; po ; [dmbst]
  | [dmbst]; po; [W]
  | [dmbld]; po; [R|W]
  | [L]; po; [A]
  | [A | Q]; po; [R | W]
  | [R | W]; po; [L]
  | [ISB] ; po ; [R]

(* Ordered-before *)
let ob1 = obs | dob | aob | bob
let ob = ob1^+

(* Internal visibility requirement *)
acyclic po-loc | fr | co | rf as internal

(* External visibility requirement *)
irreflexive ob as external

(* Atomic: Basic LDXR/STXR constraint to forbid intervening writes. *)
empty rmw & (fre; coe) as atomic
"VMSA strongest"
include "cos.cat"
include "aarch64_mmu_common.cat"

let tob =
  | [T_f] ; instruction-order

let obs = rfe | fr | wco
  (* observing a write through a fetch or translate
   * means the write is now visible to the rest of the system
   * aka {instruction, translation}->data coherence *)
  | trf | tfr

let speculative = ctrl
  | addr; po
  | [T] ; instruction-order

let dob = addr | data
  | ctrl; [W]
  | (ctrl | (addr; po)); [ISB]
  | addr; po; [W]
  | (addr | data); rfi
  | (addr | ctrl | data); trfi
  | (ctrl | (addr; po))

let aob = rmw
  | [range(rmw)]; rfi; [A | Q]

let bob = [R | W]; po; [DMB.SY]
  | [DMB.SY]; po; [R | W]
  | [L]; po; [A]
  | [R]; po; [DMB.LD]
  | [DMB.LD]; po; [R | W]
  | [A | Q]; po; [R | W]
  | [W]; po; [DMB.ST]
  | [DMB.ST]; po; [W]
  | [R | W]; po; [L]
  | [R | W | F | C]; po; [DSB.ISH | DSB.SY]
  | [DSB.ISH | DSB.SY]; po; [R | W | F | C]
  | [DMB.SY | DSB.SY | DSB.ISH | DMB.ISH]; po; [DC]
  | [ISB] ; instruction-order  (* from the ifetch model *)

(* Ordered-before *)
let ob = (obs | dob | aob | bob | iio | tob)^+

(* Internal visibility requirement *)
acyclic po-loc | fr | co | rf | tfr | trf as internal

(* External visibility requirement *)
irreflexive ob as external

(* Atomic: Basic LDXR/STXR constraint to forbid intervening writes. *)
empty rmw & (fre; coe) as atomic
